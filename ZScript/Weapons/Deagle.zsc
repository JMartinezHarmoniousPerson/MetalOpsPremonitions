
class MO_Deagle : JMWeapon// replaces Pistol
{
 	Default
	{
		Weapon.AmmoUse 0;
		Weapon.AmmoGive 10;
		Weapon.AmmoType1 "LowCalClip";
		Weapon.AmmoType2 "MODeagleAmmo";
		Obituary "%o was clocked by %k's Desert Eagle";
		+WEAPON.WIMPY_WEAPON;
		Inventory.Pickupmessage "You got the Desert Eagle! (Slot 2)";
		Tag "Desert Eagle";
		Scale .75;
		Inventory.PickupSound "weapons/deag/draw";
		Inventory.ForbiddenTo "MO_OfficerPlayer";
		VisibleToPlayerClass "MO_SergeantPlayer";
	}
	States
	{
	ContinueSelect:
		TNT1 AAAAAAAAAAAAAAAAAA 0 A_Raise();
		Goto Ready;
	Ready:
	SelectAnimation:
		TNT1 A 0 A_StartSound("weapons/deag/draw",1);
		DGLS ABCD 1;
	ReadyToFire:
		DGLG A 1 JM_WeaponReady(WRF_ALLOWRELOAD);
		Loop;
	Deselect:
		DGLS DCBA 1;
		PSTG A 0 A_Lower(12);
		Wait;
	Select:
		TNT1 A 0;
		Goto ClearAudioAndResetOverlays;
	Fire:
		PSTG A 0 JM_CheckMag("MODeagleAmmo", "Reload");
		DGLF A 1 {
			A_FireBullets (5.6, 0, 1, 45, "UpdatedBulletPuff", FBF_NORANDOM, 0,"MO_BulletTracer",0);
			A_StartSound("weapons/deag/fire", CHAN_WEAPON);
			JM_UseAmmo("MODeagleAmmo",1);
			A_SpawnItemEx("BigGunSmoke",18,0,34,2,0,0);
			JM_CheckForQuadDamage();
			}
		PSTG A 0 A_SpawnItemEx("PistolCasing",30, 3, 33, random(-2,2), random(4,6), random(4,7), 0);
		DGLF B 1 JM_GunRecoil(-1.6, .19);
		DGLF C 1 JM_GunRecoil(-1.6, .19);
		DGLF DD 1 JM_GunRecoil(-1.6, .19);
		DGLF E 2 JM_GunRecoil(-1.6, .19);
		DGLF FFFGG 1;
 		DGLF HHIIJKLM  1 {
				If(JustPressed(BT_ATTACK))
				{SetWeaponState("Fire");}
				return JM_WeaponReady(WRF_NOFIRE|WRF_ALLOWRELOAD);
			}
		PISG A 0 A_JumpIf(PressingWhichInput(BT_ATTACK), "Fire");// A_ReFire;
		PSTG A 0 JM_CheckMag("MODeagleAmmo", "Reload");
		Goto ReadyToFire;
		
	Reload:
		PSTG A 0 A_JumpIfInventory("MODeagleAmmo",7,"ReadyToFire");
		PSTG A 0 A_JumpIfInventory("LowCalClip",1,1);
		Goto ReadyToFire;
		TNT1 A 0 A_JumpIf(CountInv("MODeagleAmmo") < 1, "ReloadNoAmmo");
		DGLR ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,1);
		DGLR EFG 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/deag/magout",CHAN_AUTO);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,1);
		DGLR HIJ 1;
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,1);
		DGLR KLL 1 JM_WeaponReady(WRF_NOFIRE);
		DGLR L 14 
		{
			JM_WeaponReady(WRF_NOFIRE);
			if(CountInv("MO_PowerSpeed") == 1) {A_SetTics(7);}
		}
		DGLR M 1 
		{
			JM_WeaponReady(WRF_NOFIRE);
		}
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DGLR NO 1 JM_WeaponReady(WRF_NOFIRE);
		DGLR P 1 JM_WeaponReady(WRF_NOFIRE);
		TNT1 A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DGLR Q 1 JM_WeaponReady(WRF_NOFIRE);
		DGLR R 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "LowCalClip",7,1);
		DGLR STU 1 JM_WeaponReady(WRF_NOFIRE);
		DGLR U 6 
		{
			JM_WeaponReady(WRF_NOFIRE);
			if(CountInv("MO_PowerSpeed") == 1) {A_SetTics(3);}
		}
		DGLR VWXYZ 1;
		DG1R ABC 1 A_WeaponOffset(0,32);
		Goto ReadyToFire;
		
	ReloadNoAmmo:
		DG2R ABCD 1 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,1);
		DG2R EFG 1 JM_WeaponReady(WRF_NOFIRE);
		PSTG A 0 A_StartSound("weapons/deag/magout",CHAN_AUTO);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,1);
		DG2R HIJ 1;
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,1);
		DG2R KLL 1 JM_WeaponReady(WRF_NOFIRE);
		DG2R L 14 
		{
			JM_WeaponReady(WRF_NOFIRE);
			if(CountInv("MO_PowerSpeed") == 1) {A_SetTics(7);}
		}	
		DG2R L 1 A_WeaponOffset(1,32);
		DG2R M 1 
		{
			A_WeaponOffset(0,32);
			JM_WeaponReady(WRF_NOFIRE);
		}
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DG2R NO 1 JM_WeaponReady(WRF_NOFIRE);
		DG2R P 1 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_StartSound("weapons/deag/magin",CHAN_AUTO);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DG2R Q 1;
		DG2R R 1;
		PSTG A 0 JM_ReloadGun("MODeagleAmmo", "LowCalClip",7,1);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,1);
		DG2R STUVW 1 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,3);
		DG2R WXYZZ 1 JM_WeaponReady(WRF_NOFIRE);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,1);
		DG3R ABCDE 1;
		PSTR A 0 A_StartSound("weapons/deag/slideback",CHAN_AUTO);
		DG3R F 1; 
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DG3R G 1 A_WeaponOffset(-1,33);
		DG3R G 1 A_WeaponOffset(-2,34);
		DG3R H 1 A_WeaponOffset(-4,36);
		PSTF A 0 A_JumpIfInventory("MO_PowerSpeed",1,2);
		DG3R H 1 A_WeaponOffset(-3,34);
		PSTR A 0 A_StartSound("weapons/deag/slideforward",CHAN_AUTO);
		DG3R IJK 1 A_WeaponOffset(0,32);
		DG3R L 5
		{
			JM_WeaponReady(WRF_NOFIRE);
			if(CountInv("MO_PowerSpeed") == 1) {A_SetTics(3);}
		}	
		DG3R MNO 1;
		Goto ReadyToFire;
	Flash:
		TNT1 A 2 Bright A_Light1;
		Goto LightDone;
		TNT1 A 1 Bright A_Light1;
		Goto LightDone;
 	Spawn:
		DGLP Z -1;
		Stop;
	FlashKick:
		"####" A 0 A_JumpIfInventory("MO_PowerSpeed",1,"FlashKickFast");
		DGLK ABCDE 1 JM_WeaponReady();
		DGLK FGGG 1 JM_WeaponReady();
		DGLK FEDCBA 1 JM_WeaponReady();
		Goto ReadyToFire;
	FlashKickFast:
		DGLK ABCDE 1 JM_WeaponReady();
		DGLK FG 1 JM_WeaponReady();
		DGLK FEDCBA 1 JM_WeaponReady();
		Goto ReadyToFire;
	FlashAirKick:
		"####" A 0 A_JumpIfInventory("MO_PowerSpeed",1,"FlashAirKickFast");
		DGLK ABCDEF 1 JM_WeaponReady();
		DGLK GGG 2 JM_WeaponReady();
		DGLK FEDCBA 1 JM_WeaponReady();
		Goto ReadyToFire;
	FlashAirKickFast:
		DGLK ABCDEF 1 JM_WeaponReady();
		DGLK GGG 1 JM_WeaponReady();
		DGLK FEDCBA 1 JM_WeaponReady();
		Goto ReadyToFire;
	}
}

class MODeagleAmmo : Ammo
{
	Default
	{
		Inventory.Amount 0;
		Inventory.MaxAmount 7;
		Ammo.BackpackAmount 0;
		Ammo.BackpackMaxAmount 7;
		Inventory.Icon "DGLPZ0";
		+INVENTORY.IGNORESKILL;
	}
}